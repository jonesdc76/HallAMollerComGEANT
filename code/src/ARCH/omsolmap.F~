* $Header:$
* $Log:$
*
      SUBROUTINE OMAGFLDM(MAG,XX,BX)
C
C     *****************************************************************
C     *                                                               *
C     *   Define the magnetic field for simple dipoles and quadrupoles*
C     *   (Used for the Moller polarimeter)                           *
C     *                                                               *
C     *   called from: GUFLD                                          *
C     *                                                               *
C     *****************************************************************
C
      IMPLICIT NONE
#if defined OMGEANT_VERSION
      CHARACTER*80 VersionString
      DATA VersionString /
     & '$Id:$'/
#endif
      INTEGER MAG
      REAL XX(3),BX(3)
C
#include "geant321/gcunit.inc"
#include "omgeant/omcfla.inc"
#include "omgeant/omcsvol.inc"
#include "moller/cmolmag.inc"
C
      INTEGER i,molm,i1,i2,i3
      REAL    rquad,r,z,bzx(2),cphi,sphi,b0,sca
C
C     ------------------------------------------------------------------
C
      DO i=1,3
         BX(i)=0.
      END DO
      IF(MAG.LE.0.OR.MAG.GT.NMOLMAG) GO TO 999
C
      molm=MAG
C
      DO i=1,3
         IF(ABS(XX(i)).GT.PMOLMAGP(1+i,molm)/2.) GO TO 999
      END DO
C
      i1=IDIRDET(1)  ! direction along the beam
      i2=IDIRDET(2)  ! the 1-st transverse projection
      i3=IDIRDET(3)
C
      IF(MOLMAGP(1,molm).EQ.1) THEN
C
C---      A quadrupole
C
         rquad=PMOLMAGP(2,molm)/2.
         IF(rquad.GT.0) THEN
            IF(XX(i2)**2+XX(i3)**2.LT.rquad**2) THEN
               BX(i2)=-XX(i3)*AMAGPAR(5,MAG)/rquad
               BX(i3)=-XX(i2)*AMAGPAR(5,MAG)/rquad
               BX(i1)= 0.
            ENDIF
         ENDIF
C
      ELSE IF(MOLMAGP(1,molm).EQ.2) THEN
C
C---      A dipole
C
         BX(i2)=AMAGPAR(5,MAG)
         BX(i3)=0.
         BX(i1)=0.
C
      ELSE IF(MOLMAGP(1,molm).EQ.3) THEN
C
C---      A solenoid
C
         IF(SQRT(XX(i2)**2+XX(i3)**2).LT.PMOLMAGP(1+i2,molm)/2.) THEN
            BX(i1)=AMAGPAR(5,MAG)
            BX(i2)=0.
            BX(i3)=0.
         ENDIF
C
      ELSE IF(MOLMAGP(1,molm).EQ.4) THEN
C
C---      A solenoid..(axially symm field, the axis along Z) 
C---           with a field map provided
C
         z=ABS(XX(i1))
         r=SQRT(XX(i2)**2+XX(i3)**2)
         IF(r.GT.0.) THEN
            cphi=XX(i2)/r
            sphi=XX(i3)/r
         ELSE
            cphi=1.
            sphi=0.
         ENDIF
C---        Get the value from the map
         CALL OMSOLMAP(molm,z,r,bzx(1),b0)
C
         sca=1.
         IF(b0.GT.0.) sca=AMAGPAR(5,MAG)/b0
C         
         BX(i1)=bzx(1)*sca
         BX(i2)=bzx(2)*sca*cphi
         BX(i3)=bzx(3)*sca*sphi
C
      ENDIF
C
 999  RETURN
      END
