* $Header:$
* $Log:$
*
      SUBROUTINE OUINIT
C
C     ******************************************************************
C     *                                                                *
C     *       USER's routine called at the end of initialization       *
C     *                                                                *
C     *    ==>Called by : OMGINIT                                      *
C     *                                                                *
C     ******************************************************************
C
      IMPLICIT NONE
#if defined OMGEANT_VERSION
      CHARACTER*80 VersionString
      DATA VersionString /
     & '$Id:$'/
#endif
C
#include "geant321/gconst.inc"
#include "geant321/gcunit.inc"
#include "geant321/gcflag.inc"
#include "omgeant/omcfla.inc"
#include "omgeant/omcbeam.inc"
#include "moller/cmolmag.inc"
#include "moller/cescatt.inc"
C
      COMMON/CCSMOTT/ CSDMOTT(3),ALMOTT
      REAL            CSDMOTT,ALMOTT
C---                  CSMOTT(1:3) = E0 electron (LAB), Z target, A target.
C---                  ALMOTT      - exponential slope of the variable
C
      COMMON/CPHOTPR/ EBEAMPH   ! Bremstr. photoproduction: beam energy
     +               ,IPARTPH   ! Produced particle 
     +               ,TARGLPH   ! H2 target length (cm) 
     +               ,BFLUXPH   ! e- beam flux (particles per time_interval/pulse...
      REAL            EBEAMPH,TARGLPH,BFLUXPH
      INTEGER         IPARTPH
C
C
      INTEGER  IDHNEW
      REAL     TETMOLL,HSUM,CSMOTT,PHOTSPEC
      EXTERNAL TETMOLL,HSUM,CSMOTT,PHOTSPEC
C
      INTEGER i,itart,nbuf,itrttg,itrtut,nbin
      CHARACTER namtarg*20,cproc*16
      REAL    r0,atarg,chtarg,tltarg,ubuf(20),csfac,gam,ebeam,crosec
     +       ,ctstep,phmx,x1,x2,ratem,rateh,t1,t2,y1,y2,crosec1
C
      DATA r0/2.818/  ! electron radius in fm
C
C     ------------------------------------------------------------------
C
      NMOLMAG=0
      DO i=1,MXMOLMAG
        IF(MOLMAGP(1,i).GT.0) NMOLMAG=i
      END DO
C
C      WRITE(6,*) ' IESCATT=',IESCATT
C
      IF(IOMEXKIN.EQ.1) THEN
C
C---    Moller scattering
C     
         cproc='        Moeller '
         IDMOLTET=IDHNEW(-1)
         nbin=1200
         x1=COS(THETMOL(2)*PI/180.)
         x2=COS(THETMOL(1)*PI/180.)
         CALL HBFUN1(IDMOLTET,'Moller Theta$'
     +              ,nbin,x1,x2,TETMOLL)
         itart=3      !  electron target
         CALL GFPART(itart,namtarg,itrttg,atarg,chtarg,tltarg,ubuf,nbuf)
         ebeam=(BEAMOLIM(1)+BEAMOLIM(2))/2.
         gam=SQRT((ebeam+atarg)/atarg/2.)
         csfac=r0/10./gam/2.      !    sigma=(r0/gam)**2/4.*(4-sin2)/sin2**2 
         ctstep=(x2-x1)/nbin
         phmx=(PHIMOL(2)-PHIMOL(1))*PI/180.
         crosec=csfac**2*HSUM(IDMOLTET)*ctstep*phmx
         ratem=crosec*0.003*7.87/55.85*6.02*1.E-1*1.E-6/1.602E-19*26.
         rateh=crosec*100.0*0.00708/1.*6.02*1.E-1*1.E-6/1.602E-19*1.
         WRITE(LOUT,1000) cproc,THETMOL(1),THETMOL(2)
     +                   ,PHIMOL(1),PHIMOL(2),crosec
C
      ELSE IF(IOMEXKIN.EQ.2) THEN
C
         cproc='         Mott   '
C
C---       Find the exp slope 
C

         ebeam=(BEAMOLIM(1)+BEAMOLIM(2))/2.
         CSDMOTT(1)=ebeam
         CALL GFPART(MOTTARG,namtarg,itrttg,atarg,chtarg,tltarg
     +              ,ubuf,nbuf) 
         IF(itrttg.EQ.0) THEN
            WRITE(LOUT,*) '*** OUINIT error: MOTTARG is not defined'
     +                    ,MOTTARG
            atarg=1.
            chtarg=1.
            IEORUN=1
         ENDIF
         CSDMOTT(2)=chtarg
         CSDMOTT(3)=atarg
         ALMOTT=0.
C
         nbin=1000
         phmx=(PHIMOT(2)-PHIMOT(1))*PI/180.
C
         t1=THETMOT(1)*PI/180.
         t2=THETMOT(2)*PI/180.
         IDMOTT=IDHNEW(-1)
         CALL HBFUN1(IDMOTT,'Mott vs theta',nbin,t1,t2,CSMOTT)
         crosec=HSUM(IDMOTT)*(t2-t1)/nbin*phmx
         crosec1=0.
         WRITE(LOUT,1200) cproc,ratem,cproc,rateh
C
         IF(IMOTTWG.NE.2) THEN
            y1=CSMOTT(t1)
            y2=CSMOTT(t2)
            ALMOTT=-(ALOG(y2)-ALOG(y1))/(t2-t1)
            x1=EXP(-ALMOTT*t2)
            x2=EXP(-ALMOTT*t1)
C
            CALL HDELET(IDMOTT)
            CALL HBFUN1(IDMOTT,'Mott vs exp(-aph*thet)'
     +                 ,nbin,x1,x2,CSMOTT)
            crosec1=HSUM(IDMOTT)*(x2-x1)/nbin*phmx
            ALPHMOTT=ALMOTT
            WRITE(LOUT,1050) cproc,ALMOTT,x1,x2,y1,y2
         ENDIF          
C     
         ratem=crosec*0.003*7.87/55.85*6.02*1.E-1*1.E-6/1.602E-19
         IF(CSDMOTT(2).LT.1.1) ratem=ratem*26.**2
         rateh=crosec*100.0*0.07080/1.*6.02*1.E-1*1.E-6/1.602E-19
         WRITE(LOUT,1100) cproc,THETMOT(1),THETMOT(2)
     +                   ,PHIMOT(1),PHIMOT(2),crosec,crosec1
         WRITE(LOUT,1200) cproc,ratem,cproc,rateh
C
      ELSE IF(IOMEXKIN.EQ.3) THEN
C
         cproc='Bremst. photopr.'
         IDPHOTPR=IDHNEW(-1)
         EBEAMPH=(BEAMOLIM(1)+BEAMOLIM(2))/2.
         IPARTPH=IPHOTPR
         BFLUXPH=1.E11
         TARGLPH=150.
         CALL HBFUN2(IDPHOTPR,'Photo-prod theta/pmom'
     +       ,100,THETPHPR(1),THETPHPR(2)
     +       ,100,PMOMPHPR(1),PMOMPHPR(2),PHOTSPEC)
C         
         rateh=HSUM(IDPHOTPR)*(THETPHPR(2)-THETPHPR(1))/100
     +                       *(PMOMPHPR(2)-PMOMPHPR(1))/100
         WRITE(LOUT,1300) cproc,IPARTPH,rateh
     +                         ,THETPHPR(1),THETPHPR(2)
     +                         ,PMOMPHPR(1),PMOMPHPR(2)
     +                         ,TARGLPH,BFLUXPH
C
      ENDIF
C
      RETURN
 1000 FORMAT(/' === ',A16,' cross-section '
     +     ,F7.2,'<theta CM<',F7.2,2X,F7.2,'<phi CM<',F7.2,' deg'
     +     ,' is ',E12.4,' barn per one target electron')
 1050 FORMAT( ' === ',A16,' cross-section '
     +     ,' x=exp(-',E12.4,'*theta), x1,x2=',4E12.4)
 1100 FORMAT(/' === ',A16,' cross-section '
     +     ,F11.6,'<theta LAB<',F11.6,2X,F7.2,'<phi LAB<',F7.2,' deg'
     +     ,' are ',E12.4,2X,E12.4,' barn per one target nucleus')
 1200 FORMAT(' === ',A16,' event rate/sec =',E12.4
     +     ,' for 0.003cm Fe target, 1.microA'
     +     /,' === ',A16,' event rate/sec =',E12.4
     +     ,' for 100.0cm H2 target, 1.microA')
 1300 FORMAT(/' === ',A16,' particle',I2,' rate=',E12.3,'  for '
     +     ,F11.6,'<theta LAB<',F11.6,2X,F7.3,'<P/Pbeam LAB<',F7.3
     +     ,' Liquid H2 target ',F5.1,'cm',3X,E10.3,' beam particles')
      END
C
      REAL FUNCTION TETMOLL(COSTHET)
C
C===      Moller COS(Theta) distrubution
C
      REAL COSTHET,s2
C
      s2=SQRT(1.-COSTHET**2)
      TETMOLL=((4.-s2)/s2)**2
C
      RETURN
      END
